targetScope = 'subscription'

@minLength(1)
@maxLength(64)
@description('Name of the the environment which is used to generate a short unique hash used in all resources.')
param environmentName string

@minLength(1)
@description('Location for the OpenAI resource')
// https://learn.microsoft.com/azure/ai-services/openai/concepts/models?tabs=python-secure%2Cglobal-standard%2Cstandard-chat-completions#models-by-deployment-type
@allowed([
  'eastus2'
  'swedencentral'
])
@metadata({
  azd: {
    type: 'location'
  }
})
param location string

@description('Id of the principal to assign database and application roles.')
param deploymentUserPrincipalId string = ''

@description('Enable create-index database and containers. Leave empty for vector search only. Set to "HotelsCreateIndex" to enable both vector search and create-index scenarios.')
param createIndexDatabaseName string = ''

var resourceToken = toLower(uniqueString(subscription().id, environmentName, location))
var tags = { 'azd-env-name': environmentName }
var prefix = '${environmentName}${resourceToken}'

// Azure OpenAI model and configuration variables
//
// QUOTA REQUIREMENT: Both models below require available quota in the target
// region (eastus2 or swedencentral). If deployment fails with
// "InsufficientQuota" or "The specified capacity ... is not available", try:
//   1. A different allowed region (change the @allowed list above)
//   2. A different SKU (e.g., swap 'Standard' ↔ 'GlobalStandard')
//   3. Requesting a quota increase in the Azure Portal under
//      Subscriptions > Resource providers > Microsoft.CognitiveServices > Quotas

// Chat model: gpt-4.1-mini, version 2025-04-14, deployed as Standard
// gpt-4o-mini Standard was deprecated 2026-03-31; gpt-4o-mini GlobalStandard has
// 0 quota in this subscription. gpt-4.1-mini Standard has 460K+ quota in eastus2.
var chatModelName = 'gpt-4.1-mini'
var chatModelVersion = '2025-04-14'
var chatModelApiVersion = '2024-08-01-preview'
var chatModelSkuName = 'Standard'
var chatModelCapacity = 50

// Embedding model: text-embedding-3-small, version 1, deployed as Standard
// This is the model used by all language samples to generate 1536-dimension vectors.
var embeddingModelName = 'text-embedding-3-small'
var embeddingModelVersion = '1'
var embeddingModelApiVersion = '2024-08-01-preview'
var embeddingModelSkuName = 'Standard'
var embeddingModelCapacity = 10

// Organize resources in a resource group
resource resourceGroup 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: '${environmentName}-${resourceToken}-rg'
  location: location
  tags: tags
}

module managedIdentity 'br/public:avm/res/managed-identity/user-assigned-identity:0.4.0' = {
  name: 'user-assigned-identity'
  scope: resourceGroup
  params: {
    name: 'managed-identity-${prefix}'
    location: location
    tags: tags
  }
}

// Data and embedding configuration
var dataFileWithVectors = './data/HotelsData_toCosmosDB_Vector.json'
var dataFileWithVectorsAndRegions = './data/HotelsData_toCosmosDB_Vector_byRegion.json'
var dataFileWithoutVectors = './data/HotelsData_toCosmosDB.JSON'
var databaseName = 'Hotels'
var fieldToEmbed = 'Description'
var embeddedFieldName = 'DescriptionVector'
var embeddingDimensions = '1536'
var embeddingBatchSize = '16'
var loadSizeBatch = '50'

var openAiServiceName = 'openai-${prefix}'
module openAi 'br/public:avm/res/cognitive-services/account:0.7.1' = {
  name: 'openai'
  scope: resourceGroup
  params: {
    name: openAiServiceName
    location: location
    tags: tags
    kind: 'OpenAI'
    sku: 'S0'
    customSubDomainName: openAiServiceName
    networkAcls: {
      defaultAction: 'Allow'
      bypass: 'AzureServices'
    }
    deployments: [
      {
        name: chatModelName
        model: {
          format: 'OpenAI'
          name: chatModelName
          version: chatModelVersion
        }
        sku: {
          name: chatModelSkuName
          capacity: chatModelCapacity
        }
      }
      {
        name: embeddingModelName
        model: {
          format: 'OpenAI'
          name: embeddingModelName
          version: embeddingModelVersion
        }
        sku: {
          name: embeddingModelSkuName
          capacity: embeddingModelCapacity
        }
      }
    ]
    roleAssignments: concat(
      [
        {
          principalId: managedIdentity.outputs.principalId
          roleDefinitionIdOrName: 'Cognitive Services OpenAI User'
        }
      ],
      !empty(deploymentUserPrincipalId)
        ? [
            {
              principalId: deploymentUserPrincipalId
              roleDefinitionIdOrName: 'Cognitive Services OpenAI User'
            }
          ]
        : []
    )
  }
}

module database './database.bicep' = {
  name: 'database'
  scope: resourceGroup
  params: {
    accountName: 'db-${prefix}'
    location: location
    tags: tags
    managedIdentityPrincipalId: managedIdentity.outputs.principalId
    deploymentUserPrincipalId: deploymentUserPrincipalId
    databaseName: databaseName
    createIndexDatabaseName: createIndexDatabaseName
  }
}


output AZURE_LOCATION string = location
output AZURE_TENANT_ID string = tenant().tenantId
output AZURE_RESOURCE_GROUP string = resourceGroup.name

// Specific to Azure OpenAI
output AZURE_OPENAI_SERVICE string = openAi.outputs.name
output AZURE_OPENAI_ENDPOINT string = openAi.outputs.endpoint

output AZURE_OPENAI_CHAT_MODEL string = chatModelName
output AZURE_OPENAI_CHAT_DEPLOYMENT string = chatModelName
output AZURE_OPENAI_CHAT_ENDPOINT string = openAi.outputs.endpoint
output AZURE_OPENAI_CHAT_API_VERSION string = chatModelApiVersion

output AZURE_OPENAI_EMBEDDING_MODEL string = embeddingModelName
output AZURE_OPENAI_EMBEDDING_DEPLOYMENT string = embeddingModelName
output AZURE_OPENAI_EMBEDDING_ENDPOINT string = openAi.outputs.endpoint
output AZURE_OPENAI_EMBEDDING_API_VERSION string = embeddingModelApiVersion

output AZURE_COSMOSDB_ACCOUNT_NAME string = database.outputs.accountName
output AZURE_COSMOSDB_ENDPOINT string =  database.outputs.endpoint
output AZURE_COSMOSDB_DATABASENAME string = databaseName
output AZURE_COSMOSDB_DISKANN_CONTAINER_NAME string = empty(createIndexDatabaseName) ? database.outputs.containers[0].name : ''
output AZURE_COSMOSDB_QUANTIZEDFLAT_CONTAINER_NAME string = empty(createIndexDatabaseName) ? database.outputs.containers[1].name : ''
output AZURE_COSMOSDB_PARTITION_KEY_PATH string = database.outputs.partitionKeyPathForVectorSearch

output AZURE_COSMOSDB_CREATE_INDEX_DATABASENAME string = !empty(createIndexDatabaseName) ? createIndexDatabaseName : ''
// NOTE: CREATE-INDEX containers are created by post-provision hook (post-provision.sh/.ps1)
// because they are scenario-dependent (diskANN vs quantizedFlat). The hook copies data files
// and the sample code (control-plane.ts) creates containers with vector indexes dynamically.
// These names are runtime configuration for containers created by the samples.
output AZURE_COSMOSDB_CREATE_INDEX_DISKANN_CONTAINER_NAME string = 'hotels_diskann'
output AZURE_COSMOSDB_CREATE_INDEX_QUANTIZEDFLAT_CONTAINER_NAME string = 'hotels_quantizedflat'
output AZURE_COSMOSDB_CREATE_INDEX_EMBEDDED_FIELD string = database.outputs.embeddedFieldNameForCreateIndex
output AZURE_COSMOSDB_CREATE_INDEX_PARTITION_KEY_PATH string = database.outputs.partitionKeyPathForCreateIndex
output AZURE_COSMOSDB_CREATE_INDEX_EMBEDDING_DIMENSIONS string = '1536'

// Configuration for embedding creation and vector search
output DATA_FILE_WITH_VECTORS string = dataFileWithVectors
output DATA_FILE_WITHOUT_VECTORS string = dataFileWithoutVectors
output DATA_FILE_WITH_VECTORS_AND_REGIONS string = dataFileWithVectorsAndRegions
output FIELD_TO_EMBED string = fieldToEmbed
output EMBEDDED_FIELD string = embeddedFieldName
output EMBEDDING_DIMENSIONS string = embeddingDimensions
output EMBEDDING_BATCH_SIZE string = embeddingBatchSize
output LOAD_SIZE_BATCH string = loadSizeBatch
