targetScope = 'subscription'

@minLength(1)
@maxLength(64)
@description('Name of the the environment which is used to generate a short unique hash used in all resources.')
param environmentName string

@minLength(1)
@description('Location for all resources')
@allowed([
  'eastus2'
  'swedencentral'
])
@metadata({
  azd: {
    type: 'location'
  }
})
param location string

@description('Location for Azure OpenAI resource (defaults to main location if not specified). Not all models are available in all regions.')
// https://learn.microsoft.com/azure/ai-services/openai/concepts/models?tabs=python-secure%2Cglobal-standard%2Cstandard-chat-completions#models-by-deployment-type
@allowed([
  'eastus'
  'eastus2'
  'eastus3'
  'westus'
  'westus2'
  'westus3'
  'northeurope'
  'swedencentral'
])
@metadata({ azd: { type: 'location' } })
param openAiLocation string = location

@description('Id of the principal to assign database and application roles.')
param deploymentUserPrincipalId string = ''

var resourceToken = toLower(uniqueString(subscription().id, environmentName, location))
var tags = { 'azd-env-name': environmentName }
var prefix = '${environmentName}${resourceToken}'

// Azure OpenAI model and configuration parameters
// https://learn.microsoft.com/azure/ai-services/openai/concepts/models?tabs=python-secure%2Cglobal-standard%2Cstandard-chat-completions#models-by-deployment-type
// To change deployment type, swap 'Standard' ↔ 'GlobalStandard' in the sku name parameters below.
// gpt-4o-mini Standard was deprecated 2026-03-31; use gpt-4.1-mini instead.

@description('Chat model name')
param chatModelName string = 'gpt-4.1-mini'

@description('Chat model version')
param chatModelVersion string = '2025-04-14'

@description('Chat model deployment type: Standard or GlobalStandard')
param chatModelSkuName string = 'Standard'

var chatModelApiVersion = '2024-08-01-preview'
var chatModelCapacity = 50

@description('Embedding model name')
param embeddingModelName string = 'text-embedding-3-small'

@description('Embedding model version')
param embeddingModelVersion string = '1'

@description('Embedding model deployment type: Standard or GlobalStandard')
param embeddingModelSkuName string = 'Standard'

var embeddingModelApiVersion = '2024-08-01-preview'
var embeddingModelCapacity = 10

// Organize resources in a resource group
resource resourceGroup 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: '${environmentName}-${resourceToken}-rg'
  location: location
  tags: tags
}

module managedIdentity 'br/public:avm/res/managed-identity/user-assigned-identity:0.4.0' = {
  name: 'user-assigned-identity'
  scope: resourceGroup
  params: {
    name: 'managed-identity-${prefix}'
    location: location
    tags: tags
  }
}

// Data and embedding configuration
var dataFileWithVectors = '../data/HotelsData_toCosmosDB_Vector.json'
var dataFileWithoutVectors = '../data/HotelsData_toCosmosDB.JSON'
var databaseName = 'Hotels'
var fieldToEmbed = 'Description'
var embeddedFieldName = 'DescriptionVector'
var embeddingDimensions = '1536'
var embeddingBatchSize = '16'
var loadSizeBatch = '50'

var openAiServiceName = 'openai-${prefix}'
module openAi 'br/public:avm/res/cognitive-services/account:0.10.0' = {
  name: 'openai'
  scope: resourceGroup
  params: {
    name: openAiServiceName
    location: openAiLocation
    tags: tags
    kind: 'OpenAI'
    sku: 'S0'
    customSubDomainName: openAiServiceName
    networkAcls: {
      defaultAction: 'Allow'
      bypass: 'AzureServices'
    }
    deployments: [
      {
        name: chatModelName
        model: {
          format: 'OpenAI'
          name: chatModelName
          version: chatModelVersion
        }
        sku: {
          name: chatModelSkuName
          capacity: chatModelCapacity
        }
      }
      {
        name: embeddingModelName
        model: {
          format: 'OpenAI'
          name: embeddingModelName
          version: embeddingModelVersion
        }
        sku: {
          name: embeddingModelSkuName
          capacity: embeddingModelCapacity
        }
      }
    ]
    roleAssignments: concat(
      [
        {
          principalId: managedIdentity.outputs.principalId
          roleDefinitionIdOrName: 'Cognitive Services OpenAI User'
        }
      ],
      !empty(deploymentUserPrincipalId)
        ? [
            {
              principalId: deploymentUserPrincipalId
              roleDefinitionIdOrName: 'Cognitive Services OpenAI User'
            }
          ]
        : []
    )
  }
}

module database './database.bicep' = {
  name: 'database'
  scope: resourceGroup
  params: {
    accountName: 'db-${prefix}'
    location: location
    tags: tags
    managedIdentityPrincipalId: managedIdentity.outputs.principalId
    deploymentUserPrincipalId: deploymentUserPrincipalId
    databaseName: databaseName
  }
}


output AZURE_LOCATION string = location
output AZURE_TENANT_ID string = tenant().tenantId
output AZURE_RESOURCE_GROUP string = resourceGroup.name

// Specific to Azure OpenAI
output AZURE_OPENAI_SERVICE string = openAi.outputs.name
output AZURE_OPENAI_ENDPOINT string = openAi.outputs.endpoint

output AZURE_OPENAI_CHAT_MODEL string = chatModelName
output AZURE_OPENAI_CHAT_DEPLOYMENT string = chatModelName
output AZURE_OPENAI_CHAT_ENDPOINT string = openAi.outputs.endpoint
output AZURE_OPENAI_CHAT_API_VERSION string = chatModelApiVersion

output AZURE_OPENAI_EMBEDDING_MODEL string = embeddingModelName
output AZURE_OPENAI_EMBEDDING_DEPLOYMENT string = embeddingModelName
output AZURE_OPENAI_EMBEDDING_ENDPOINT string = openAi.outputs.endpoint
output AZURE_OPENAI_EMBEDDING_API_VERSION string = embeddingModelApiVersion

// Environment variables needed by utils.ts
output AZURE_COSMOSDB_ENDPOINT string =  database.outputs.endpoint
output AZURE_COSMOSDB_DATABASENAME string = databaseName

// Configuration for embedding creation and vector search
output DATA_FILE_WITH_VECTORS string = dataFileWithVectors
output DATA_FILE_WITHOUT_VECTORS string = dataFileWithoutVectors
output FIELD_TO_EMBED string = fieldToEmbed
output EMBEDDED_FIELD string = embeddedFieldName
output EMBEDDING_DIMENSIONS string = embeddingDimensions
output EMBEDDING_BATCH_SIZE string = embeddingBatchSize
output LOAD_SIZE_BATCH string = loadSizeBatch
